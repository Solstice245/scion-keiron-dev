//==================================================================================================
//  Scion Genetron Melee AI
//==================================================================================================

include "TriggerLibs/Genetron/GenetronVyEy"
include "TriggerLibs/Genetron/GenetronEasy"
include "TriggerLibs/Genetron/GenetronMedi"
include "TriggerLibs/Genetron/GenetronMdHd"
include "TriggerLibs/Genetron/GenetronHard"
//include "TriggerLibs/Genetron/GenetronHdVH"
//include "TriggerLibs/Genetron/GenetronVyHd"
//include "TriggerLibs/Genetron/GenetronChVi"
//include "TriggerLibs/Genetron/GenetronChRe"
//include "TriggerLibs/Genetron/GenetronChIn"

//--------------------------------------------------------------------------------------------------
//  Counter-Attack Units
//--------------------------------------------------------------------------------------------------
static void InitCounters (int player) {
    // versus Protoss
    AICounterUnitSetup(player, c_PU_Zealot,             1.00, c_GU_Fixer,       1.00, c_GU_Fixer);
    AICounterUnitSetup(player, c_PU_Stalker,            1.00, c_GU_Equalizer,   1.00, c_GU_Equalizer);
    AICounterUnitSetup(player, c_PU_Sentry,             1.00, c_GU_Blitzer,     1.00, c_GU_Blitzer);
    AICounterUnitSetup(player, c_PU_HighTemplar,        1.00, c_GU_Blitzer,     1.00, c_GU_Tesla);
    AICounterUnitSetup(player, c_PU_DarkTemplar,        1.50, c_GU_Blitzer,     1.50, c_GU_Blitzer);
    AICounterUnitSetup(player, c_PU_Archon,             1.00, c_GU_Virtus,      1.00, c_GU_Virtus);
    AICounterUnitSetup(player, c_PU_Immortal,           4.00, c_GU_Spitfire,    4.00, c_GU_Spitfire);
    AICounterUnitSetup(player, c_PU_Colossus,           2.00, c_GU_Mole_Alias,  1.00, c_GU_Virtus);
    AICounterUnitSetup(player, c_PU_Phoenix,            2.00, c_GU_Blitzer,     0.50, c_GU_Torrent);
    AICounterUnitSetup(player, c_PU_VoidRay,            3.00, c_GU_Blitzer,     0.50, c_GU_Torrent);
    AICounterUnitSetup(player, c_PU_Carrier,            3.00, c_GU_Torrent,     3.00, c_GU_Torrent);
    AICounterUnitSetup(player, c_PB_PhotonCannon,       3.00, c_GU_Equalizer,   1.00, c_GU_Jupiter);
    AICounterUnitSetup(player, c_PU_Mothership,         5.00, c_GU_Torrent,     5.00, c_GU_Torrent);
    AICounterUnitSetup(player, c_PU_MothershipCore,     1.00, c_GU_Torrent,     1.00, c_GU_Torrent);
    AICounterUnitSetup(player, c_PU_Oracle,             1.00, c_GU_Torrent,     1.00, c_GU_Torrent);
    AICounterUnitSetup(player, c_PU_Tempest,            3.00, c_GU_Torrent,     3.00, c_GU_Torrent);
    AICounterUnitSetup(player, c_PU_Adept,              1.00, c_GU_Blitzer,     1.00, c_GU_Blitzer);
    AICounterUnitSetup(player, c_PU_Disruptor,          0.75, c_GU_Virtus,      0.75, c_GU_Virtus);

    // versus Terran
    AICounterUnitSetup(player, c_TU_Marine,             1.00, c_GU_Spitfire,    0.50, c_GU_Fixer);
    AICounterUnitSetup(player, c_TU_Marauder,           1.00, c_GU_Spitfire,    0.50, c_GU_Equalizer);
    AICounterUnitSetup(player, c_TU_Reaper,             0.60, c_GU_Spitfire,    0.60, c_GU_Blitzer);
    AICounterUnitSetup(player, c_TU_Ghost,              0.50, c_GU_Blitzer,     0.50, c_GU_Tesla);
    AICounterUnitSetup(player, c_TU_Hellion,            0.60, c_GU_Blitzer,     0.60, c_GU_Blitzer);
    AICounterUnitSetup(player, c_TU_SiegeTank_Alias,    1.00, c_GU_Mole_Alias,  0.50, c_GU_Jupiter);
    AICounterUnitSetup(player, c_TU_Thor,               4.00, c_GU_Equalizer,   1.00, c_GU_Mole_Alias);
    AICounterUnitSetup(player, c_TU_Viking_Alias,       2.00, c_GU_Blitzer,     0.50, c_GU_Torrent);
    AICounterUnitSetup(player, c_TU_Medivac,            0.60, c_GU_Avenger,     0.60, c_GU_Torrent);
    AICounterUnitSetup(player, c_TU_Raven,              0.50, c_GU_Avenger,     0.50, c_GU_Tesla);
    AICounterUnitSetup(player, c_TU_Banshee,            0.60, c_GU_Avenger,     0.60, c_GU_Torrent);
    AICounterUnitSetup(player, c_TU_Battlecruiser,      2.50, c_GU_Avenger,     2.50, c_GU_Torrent);
    AICounterUnitSetup(player, c_TB_Bunker,             3.00, c_GU_Equalizer,   1.00, c_GU_Jupiter);
    AICounterUnitSetup(player, c_TU_WidowMine,          0.75, c_GU_Blitzer,     0.75, c_GU_Blitzer);
    AICounterUnitSetup(player, c_TU_Cyclone,            1.00, c_GU_Spitfire,    1.00, c_GU_Equalizer);
    AICounterUnitSetup(player, c_TU_Liberator_Alias,    1.00, c_GU_Torrent,     0.50, c_GU_Avenger);

    // versus Zerg
    AICounterUnitSetup(player, c_ZU_Zergling,           0.60, c_GU_Spitfire,    0.30, c_GU_Fixer);
    AICounterUnitSetup(player, c_ZU_Baneling,           0.50, c_GU_Blitzer,     0.50, c_GU_Blitzer);
    AICounterUnitSetup(player, c_ZU_Roach,              0.80, c_GU_Equalizer,   0.80, c_GU_Equalizer);
    AICounterUnitSetup(player, c_ZU_Hydralisk,          2.00, c_GU_Spitfire,    0.50, c_GU_Inciter);
    AICounterUnitSetup(player, c_ZU_Infestor,           1.50, c_GU_Blitzer,     1.50, c_GU_Tesla);
    AICounterUnitSetup(player, c_ZU_Mutalisk,           1.50, c_GU_Blitzer,     0.75, c_GU_Boar);
    AICounterUnitSetup(player, c_ZU_Corruptor,          1.00, c_GU_Blitzer,     0.40, c_GU_Torrent);
    AICounterUnitSetup(player, c_ZU_Ultralisk,          2.00, c_GU_Equalizer,   1.00, c_GU_Virtus);
    AICounterUnitSetup(player, c_ZU_BroodLord,          2.00, c_GU_Avenger,     2.00, c_GU_Avenger);
    AICounterUnitSetup(player, c_ZB_SpineCrawler,       3.00, c_GU_Equalizer,   1.00, c_GU_Jupiter);
    AICounterUnitSetup(player, c_ZU_SwarmHost,          2.00, c_GU_Fixer,       1.00, c_GU_Inciter);
    AICounterUnitSetup(player, c_ZU_Viper,              1.00, c_GU_Avenger,     1.00, c_GU_Torrent);
    AICounterUnitSetup(player, c_ZU_Ravager,            1.00, c_GU_Blitzer,     1.00, c_GU_Blitzer);
    AICounterUnitSetup(player, c_ZU_Lurker,             2.00, c_GU_Equalizer,   1.00, c_GU_Mole_Alias);
}

//--------------------------------------------------------------------------------------------------
//  GenetronSubStateName
//--------------------------------------------------------------------------------------------------
string GenetronSubStateName (int state) {
    // TODO Call the individual difficulties to return a real substate name
    return "-" + IntToString(state) + "-";
}

//--------------------------------------------------------------------------------------------------
//  DebugCallbackGen
//--------------------------------------------------------------------------------------------------
void DebugCallbackGen (int player) {
    DebugAI("=====GENETRON=====\n");
    DebugMelee(player);
    DebugAI("e_mainState = "        + MainStateName(AIState(player, e_mainState))           +
            ", e_mainSubState = "   + GenetronSubStateName(AIState(player, e_mainSubState)) +
            ", e_attackState = "    + AttackStateName(AIState(player, e_attackState))
    );
}

//--------------------------------------------------------------------------------------------------
//  AINewUnitGen
//--------------------------------------------------------------------------------------------------
void AINewUnitGen (int player, unit u) {
    wave w;
    string type = UnitGetType(u);

    // ignored units
    //
    if (UnitTypeTestAttribute(type, c_unitAttributeStructure)) {
        return;
    }
    if (UnitTypeTestFlag(type, c_unitFlagWorker)) {
        return;
    }

    // units that stay near home
    //
    if (type == c_GU_Courier) {
        AIWaveAddUnitPriority(AIWaveGet(player, c_waveHome), u, c_prioWavePeon);
        return;
    }

    // clear obstacle units
    //
    if (AIWaveNeedClearObsUnits(player)) {
        if (type == c_GU_Spitfire || type == c_GU_Blitzer || type == c_GU_Equalizer) {
            AIMergeUnit(player, u, AIWaveGet(player, c_waveClearObs));
            return;
        }
    }

    // diversion units
    if (AIGetFlag(player, e_flagsDiversion)) {
        if (type == c_GU_Inciter) {
            AIMergeUnit(player, u, AIWaveGet(player, c_waveDivert1));
            return;
        }
        if (type == c_GU_Hornet) {
            AIMergeUnit(player, u, AIWaveGet(player, c_waveDivert2));
            return;
        }
    }

    // Processor
    //
    if (type == c_GU_Processor) {

        AINewDetector(player, u, false);

        if (AINumEnemyBuildings(player) == 0) {
            if (AIOfferNewScout(player, u)) {
                return;
            }
        }

        AIWaveAddUnitPriority(AIWaveGet(player, c_waveMain), u, c_prioWavePeon);
        return;
    }

    // main wave units
    //
    AINewUnitDefault(player, u);
}

//--------------------------------------------------------------------------------------------------
//  AIGetScoutGen
//--------------------------------------------------------------------------------------------------
unit AIGetScoutGen (int player, int index, unit prev, bool allowWorkers) {
    unit Processor;
    string prevType = UnitGetType(prev);
    
    if (!PlayerBeaconIsSet(player, c_beaconScout) &&
        !AIGetFlag(player, e_flagsScouting)) {
        // if no scouting beacons are set and we're not ready to scout, don't return a unit
        return c_nullUnit;
    }

    if (index == 1 && PlayerBeaconIsSet(player, c_beaconScout)) {
        AISetFlag(player, e_flagsScouting, true);
        if (allowWorkers) {
            // only return a worker, we want to claim a tower
            if (prevType == c_GU_ACR) {
                return prev;
            } 
            return AIGrabUnit(player, c_GU_ACR, c_prioScout, null);
        }
    }

    if (AINumEnemyBuildings(player) == 0) {
        // only use medivac scouts if we don't know of any enemy buildings
        //   (ie we need a flying scout to check other islands etc).
        if (prevType == c_GU_Processor) {
            return prev;
        }
        Processor = AIGrabUnit(player, c_GU_Processor, c_prioScout, null);
        if (Processor) {
            return Processor;
        }
    }

    if (prev) {
        return prev;
    }

    // back to allowing workers as late scouts
    //if (AIGetFlag(player, e_flagsLateScout)) {
    //    return c_nullUnit;
    //}
    if (!allowWorkers) {
        return c_nullUnit;
    }
    return AIGrabUnit(player, c_GU_ACR, c_prioScout, null);
}

//--------------------------------------------------------------------------------------------------
//  AIEarlyDefScoutGen
//--------------------------------------------------------------------------------------------------
unit AIEarlyDefScoutGen (int player, unit prev, bool allowWorkers) {
    unit obs;
    string prevType; 
    
    if (!AIGetFlag(player, e_flagsEarlyDefScout)) {
        return c_nullUnit;
    }
    prevType = UnitGetType(prev);
    if (prevType == c_GU_Processor) {
        return prev;
    }
    obs = AIGrabUnit(player, c_GU_Processor, c_prioScout, null);
    if (obs) {
        return obs;
    }
    if (prevType == c_GU_Spitfire) {
        return prev;
    }
    obs = AIGrabUnit(player, c_GU_Spitfire, c_prioScout, null);
    if (obs) {
        return obs;
    }
    if (prev) {
        return prev;
    }
    if (!allowWorkers) {
        return c_nullUnit;
    }
    return AIGrabUnit(player, c_GU_ACR, c_prioScout, null);
}

//--------------------------------------------------------------------------------------------------
//  AIWaveThinkGen
//--------------------------------------------------------------------------------------------------
void AIWaveThinkGen (int player, wave w, int type) {
    AIWaveThinkDefault(player, w, type);
}

//--------------------------------------------------------------------------------------------------
//  Genetron Init
//--------------------------------------------------------------------------------------------------
static void GenetronInit (int player) {

    InitCounters(player);
    AIMeleeSharedInit(player);
    AISetFlag(player, e_flagsScouting, false);
    AISetFlag(player, e_flagsEarlyDefScout, false);
    AISetDefaultArmyUnit(player, c_GU_Spitfire);
    AISetDefaultTownHall(player, c_GB_ProcessingCore);

    AISetMainState(player, e_mainState_Open, e_mainSubState_Init);

    if (AIPlayerDifficulty(player) > c_skirHard) {
        UIDisplayMessage(PlayerGroupAll(), c_messageAreaChat, StringToText("Genetron AI: Difficulties above Harder are not supported at this time, please use at most Harder difficulty for this AI"));
    }

}

//--------------------------------------------------------------------------------------------------
void GenetronOpen (int player);
void GenetronMid (int player);
void GenetronLate (int player);

//--------------------------------------------------------------------------------------------------
//  AIMeleeGen
//--------------------------------------------------------------------------------------------------
void AIMeleeGen (int player) {
    int mainState = AIState(player, e_mainState);

    if (AIPlayerDifficulty(player) >= c_skirChMoney) {
        AIMeleeCheatResources(player);
    }

    if (mainState == e_mainState_Init)      { GenetronInit(player); }
    else if (mainState == e_mainState_Open) { GenetronOpen(player); }
    else if (mainState == e_mainState_Mid ) { GenetronMid (player); }
    else if (mainState == e_mainState_Late) { GenetronLate(player); }
    else { ErrorMeleeScript(player, "Invalid mainState"); }
}


void GenetronOpen (int player) {
    int diff = AIPlayerDifficulty(player);
    
    if      (diff == c_skirVeryEasy) { GenetronOpenVyEy(player); }
    else if (diff == c_skirEasy)     { GenetronOpenEasy(player); }
    else if (diff == c_skirMedium)   { GenetronOpenMedi(player); }
    else if (diff == c_skirMed_Hard) { GenetronOpenMdHd(player); }
    else if (diff == c_skirHard)     { GenetronOpenHard(player); }
    else                             { GenetronOpenHard(player); }

}

void GenetronMid (int player) {
    int diff = AIPlayerDifficulty(player);

    if      (diff == c_skirVeryEasy) { GenetronMidVyEy(player); }
    else if (diff == c_skirEasy)     { GenetronMidEasy(player); }
    else if (diff == c_skirMedium)   { GenetronMidMedi(player); }
    else if (diff == c_skirMed_Hard) { GenetronMidMdHd(player); }
    else if (diff == c_skirHard)     { GenetronMidHard(player); }
    else                             { GenetronMidHard(player); }

}

void GenetronLate (int player) {
    int diff = AIPlayerDifficulty(player);
    
    if      (diff == c_skirVeryEasy) { GenetronLateVyEy(player); }
    else if (diff == c_skirEasy)     { GenetronLateEasy(player); }
    else if (diff == c_skirMedium)   { GenetronLateMedi(player); }
    else if (diff == c_skirMed_Hard) { GenetronLateMdHd(player); }
    else if (diff == c_skirHard)     { GenetronLateHard(player); }
    else                             { GenetronLateHard(player); }

}
